/*
 *	Grammar
 *
 *	Abreviações:
 *		- {something} significa sequência de zero ou mais 'something'
 *		- [something] siignifica que 'something' é opcional
 */

program
	: {definition}

definition
	: function-definition
	| monitor-definition

function-definition
	: 'function' ID parameters [type] block

parameters
	: '(' [parameter {',' parameter}] ')'

parameter
	: variable-declaration

type
	: ID
	| array-type
	| base-type

array-type
	: '[' type ']'

base-type
	: 'Char'
	| 'Int'
	| 'Float'

block
	: '{' {block-content} '}'

block-content
	: variable-declaration
	| command

variable-declaration
	: type ID {',' ID}

command
	: 'if' expression '{' command '}'
	| 'if' expression '{' command '}' 'else' command
	| 'while' expression command
	| variable '=' expression
	| 'return' [expression]
	| function-call
	| 'spawn' block
	| block

expression
	: expression-or

expression-or
	: expression-or 'or' expression-and
	| expression-and

expression-and
	: expression-and 'and' expression-comparison
	| expression-comparison

expression-comparison
	: expression-comparison '==' expression-addition
	| expression-comparison '<=' expression-addition
	| expression-comparison '>=' expression-addition
	| expression-comparison '<' expression-addition
	| expression-comparison '>' expression-addition
	| expression-addition

expression-addition
	: expression-addition '+' expression-multiplication
	| expression-addition '-' expression-multiplication
	| expression-multiplication

expression-multiplication
	: expression-multiplication '*' expression-unary
	| expression-multiplication '/' expression-unary
	| expression-unary

expression-unary
	: '-' expression-primary
	| 'not' expression-unary
	| expression-primary

expression-primary
	: literal
	| variable
	| function-call
	| array-type '(' expression ')' 	// new array
	| type arguments					// constructor call
	| '(' expression ')'

literal
	: INT
	| FLOAT
	| STRING

variable
	: ID
	| expression '[' expression ']'

function-call
	: ID arguments
	| expression-primary '.' ID arguments

arguments
	: '(' [expression {',' expression}] ')'

monitor-definition
	: 'monitor' ID {monitor-block}

monitor-block
	: '{' {monitor-block-content} '}'

monitor-block-content
	: attribute-declaration
	| constructor-definition
	| method-definition

attribute-declaration
	: 'private' variable-declaration

constructor-definition
	: 'init' parameters block

method-definition
	: ['private'] 'function' ID parameters [type] monitor-method-block

monitor-method-block
	: '{' {variable-declaration} {monitor-method-command} '}'

monitor-method-command
	: 'wait' 'in' variable? 'until' function-call?
	| 'signal' variable?
	| 'broadcast' variable?
	| command
